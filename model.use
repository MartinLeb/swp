model Auction

--------------------------------------------
--enums
--------------------------------------------

-- no distinction between general and auction-specific commands here,
-- doesn't seem to make sense on this level
enum CommandWord {
	login, register, bid, joinAuction, createAuction, setStrategy
}

--------------------------------------------
--classes
--------------------------------------------

abstract class Agent
attributes
	name: String
	id: Integer
	description: String
operations
	sendMessage(m: Message, receiver: Agent)
end

class AuctioneerAgent < Agent
operations
	generateUserID(): Integer
	generateAuctionID(): Integer
	generateAuctionAgent(auctionID: Integer)
end

class BidAgent < Agent
end

class AuctionAgent < Agent
attributes
	startTime: Integer
	endTime: Integer
end

class SearchAgent < Agent
end

class AuctionDescription
attributes
	productDescription: String
end

abstract class AuctionType
attributes
	currentBid: Real
operations
	acceptBid(amount: Real): Boolean
end

class EnglishAuction < AuctionType
attributes 
	minBid: Real
	priceIncrement: Real
end

class DutchAuction < AuctionType
attributes
	startPrice: Real
	priceDecrement: Real
end

class Bid
attributes
	user: User
	price: Real
end

class BidHistory
end

class User
attributes
	loginName: String
	password: String
	eMail: String
	id: Integer
operations
	createBidAgent()
end

-- this is just one message, for an actual implementation we need many more
-- message types for different payloads, but they wouldn't help here
class Message
attributes
	command: CommandWord
end

class Category
attributes
	name: String
	id: Integer
end

class Object
attributes
	name: String
	description: String
end


abstract class Strategy
attributes
	bidPrice: Real
end

class BidByTime < Strategy
attributes
	time: Integer
end

class PeriodicBid < Strategy
attributes
	startTime:Integer
	interval: Integer
	maxBid: Real
end

--------------------------------------------
--compositions
--------------------------------------------
-- some of the names here are very (too) verbose, to avoid awkward situations
-- like having "has" twice (USE can't handle duplicate association names) 

composition AuctionHasProduct between
	AuctionAgent[1] 
	Object[1]	
end

composition saves between
	AuctionAgent[1]
	BidHistory[1]
end

composition contains between
	BidHistory[1]
	Bid[*]
end
	
--------------------------------------------
--aggregations
--------------------------------------------

aggregation ObjectHasCategory between
	Object[*]
	Category[1]
end

aggregation follows between
	BidAgent[0..*]
	Strategy[1]
end

aggregation AuctionHasType between
	AuctionAgent[1..*]
	AuctionType[1]
end

aggregation finds between
	SearchAgent[1]
	AuctionDescription[*]
end

aggregation AuctioneerKnowsAllAuctions between
	AuctioneerAgent[1]
	AuctionDescription[*]
end

--------------------------------------------
--associations
--------------------------------------------

association uses between
	Agent[1]
	Message[*]
end

association CategoryHierarchy between
	Category[1] role Category
	Category[*] role SubCategory
end

association manages between
	AuctioneerAgent[1]
	AuctionAgent[0..*]
end

association registers between
	AuctioneerAgent[0..*]
	User[0..*]
end

association searchesAuctions between
	User[*]
	SearchAgent[1..*] 
end

association queriesAuctions between
	SearchAgent[1..*] role QuerySender
	AuctioneerAgent[1] role repliesResultList
end

association creates between
	User[1] role source
	BidAgent[1] role bidder
--	AuctionAgent[1] role targetAuction -- now why doesn't this work
end

association BidAgentKnowsAuction between
	BidAgent[1]
	AuctionAgent[1]
end

association submit between
	BidAgent[1]
	Bid[0..*]
end

----------------------------------------------
--Constraints
----------------------------------------------
constraints
context AuctioneerAgent
	inv: name.isDefined and id.isDefined
context AuctionAgent
	inv: name.isDefined and id.isDefined and startTime < endTime
context Object
	inv: name.isDefined and description.isDefined